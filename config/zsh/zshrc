# PROMPT
autoload -Uz promptinit
promptinit

if [ -z $DISPLAY ]; then
    CLOCK="[%*%f] "
else
    CLOCK=""
fi

prompt_debian_setup() {
    PS1="$CLOCK%B%F{green}%n@%m%f%b:%B%F{blue}%~%f%b%# "
    PS2="%# "
}
prompt_default_setup() {
    PS1="$CLOCK%B%F{blue}%n@%m%f%b%B %F{red}%(4~|.../%3~|%~)%f%b 짜 "
    PS2="짜 "
}
prompt_server_setup() {
    PS1="$CLOCK%B%F{green}%n@%m%f%b%B %F{blue}%(4~|.../%3~|%~)%f%b 짜 "
    PS2="짜 "
}
prompt_themes+=( debian default server )

if [ "$(uname -m)" = "aarch64" ]; then
    prompt server
else
    prompt default
fi

PROMPT_EOL_MARK=""

# History
HISTFILE="$XDG_CONFIG_HOME/zsh/history"
HISTSIZE=5000
SAVEHIST=5000
HISTORY_IGNORE="(ls|cd|pwd|exit|cd *|..)"
setopt HIST_IGNORE_SPACE
setopt SHARE_HISTORY
setopt HIST_IGNORE_ALL_DUPS

autoload -U history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^[[A" history-beginning-search-backward-end
bindkey "^[[B" history-beginning-search-forward-end
bindkey "^R" history-incremental-pattern-search-backward

# Completion
autoload -Uz compinit
compinit
zstyle ":completion:*" matcher-list "m:{a-z}={A-Z}"
zstyle ":completion:*" menu select

# Directory options
setopt AUTO_CD

# Alias
alias ls="ls --color=auto --group-directories-first --show-control-chars"
alias ll="ls -la"
alias la="ls -a"
alias dir="dir --color=auto"
alias mv="mv -i"
alias rm="rm -i"
alias ps="ps --sort=start_time"
alias df="df -h"
alias free="free -m"
alias grep="grep --color=auto"
alias fgrep="fgrep --color=auto"
alias egrep="egrep --color=auto"
alias echo="echo -e"
alias cls="clear"
alias killuser="pkill -KILL -u $USER"
alias vi="nvim"
alias vim="nvim"
alias svim="sudoedit"
alias ping="ping -c 5"
alias aria2="aria2c"
alias sysinfo="fastfetch && echo"
alias edit="msedit"
alias mdl="yt-dlp -f \"bestaudio\" --extract-audio --audio-format m4a"
if command -v kioclient > /dev/null; then
    alias code="GTK_USE_PORTAL=1 ELECTRON_TRASH=kioclient code"
fi
if command -v prime-run > /dev/null; then
    alias ffmpeg="prime-run ffmpeg -hide_banner"
else
    alias ffmpeg="ffmpeg -hide_banner"
fi

# Custom commands
function mkcd() {
    if [ -d $1 ]; then
        printf -P "zsh: \"$1\" already exists!"
        cd $1
    else
        mkdir -p $1 && cd $1
    fi
}

function srm() {
    if read -q "choice?zsh: sure you want to secure delete \"$1\"?"; then
        shred -u $1
    else
        return 1
    fi
}

function restartshell() {
    if command -v plasmashell > /dev/null; then
        plasmashell --replace & disown
    else
        printf "zsh: command not supported in this environment!"
    fi
}

function activate() {
    VENV_PATH="./.venv/bin/activate"
    if [ -f $VENV_PATH ]; then
        source $VENV_PATH
    else
        printf "zsh: python environment not found!"
    fi
}

function clear-screen-scrollback() {
    echoti civis > "$TTY"
    printf "%b" "\e[H\e[2J" > "$TTY"
    zle .reset-prompt
    zle -R
    printf "%b" "\e[3J" > "$TTY"
    echoti cnorm > "$TTY"
}
zle -N clear-screen-scrollback
bindkey "^L" clear-screen-scrollback
